buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven {
            name = 'impactdevelopment-repo'
            url = 'https://impactdevelopment.github.io/maven/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.github.ImpactDevelopment', name: 'ForgeGradle', version: '3.0.115'
        classpath group: 'com.github.ImpactDevelopment', name: 'MixinGradle', version: '0.6.2'
    }
}

plugins {
    id 'de.undercouch.download' version '4.0.0'
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = '1.3'
group = 'de.rexlmanu' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'viaversion-addon'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

def mcpVersion = [channel: 'snapshot', version: '20201028-1.16.3']

configurations {
    shade
    compile.extendsFrom shade
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "network.refmap.json"
}

minecraft {
    mappings mcpVersion

    if (getProject().hasProperty("forge")) {
        reobfMappings 'searge'
    } else {
        reobfMappings 'notch'
    }

    runs {
        client {
            workingDirectory project.file('run')

            main "launch.AddonLauncher"

            environment 'assetIndex', '{asset_index}'
            environment 'assetDirectory', downloadAssets.output
            environment 'nativesDirectory', extractNatives.output

            environment 'tweakClass', 'net.labymod.vanilla.LabyModTweaker'

            jvmArg('-DdebugMode=true')
            jvmArg('-Daddonresources=addon.json')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven { url = "https://repo.viaversion.com/" }
    mavenLocal()
}


dependencies {
    annotationProcessor("org.spongepowered:mixin:0.8.2:processor")
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    minecraft 'com.github.ImpactDevelopment:Vanilla:1.16.5'

    shade("org.yaml:snakeyaml:1.28")
    shade("com.viaversion:viaversion:4.4.2") { transitive = false }
    shade("com.viaversion:viabackwards:4.4.2")
    shade("com.viaversion:viarewind-fabric:2.0.3-SNAPSHOT")

    compile(files(
            'libs/lm_api_mc1.16.5.jar',
    ))

    compileOnly 'org.projectlombok:lombok:1.18.20'

    compile("org.ow2.asm:asm-analysis:6.2") { transitive = false }
    compile("org.ow2.asm:asm-util:6.2") { transitive = false }
    compile("org.ow2.asm:asm-commons:6.2") { transitive = false }
    compile("org.spongepowered:mixin:0.8.2") { transitive = false }
    compile("net.minecraft:launchwrapper:1.12") { transitive = false }
}

jar {
    // Removing files from dependencies
    exclude(
            "mcmod.info",
            "fabric.mod.json",
            "velocity-plugin.json",
            "bungee.yml",
            "plugin.yml",
    )
    // Excludes the start file
    exclude("**/launch")
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

task downloadAPI(type: Download) {
    group("labymod")

    File libraryDirectory = new File("libs/")

    if (!libraryDirectory.exists()) {
        libraryDirectory.mkdirs()
    }

    src "https://dl.labymod.net/latest/api/files/lm_api_mc1.16.5.jar"
    dest 'libs/'
}